// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // zerox = x (zx == 0)
    //         0 (zx == 1)
    Mux16(a[0..15]=x, b=false, sel=zx, out=zerox);

    // zeroy = y (zy == 0)
    //         0 (zy == 1)
    Mux16(a[0..15]=y, b=false, sel=zy, out=zeroy);

    // x2use = zerox (nx == 0)
    //        -zerox (nx == 1)
    Not16(in[0..15]=zerox, out=negx);
    Mux16(a[0..15]=zerox, b[0..15]=negx, sel=nx, out=x2use);

    // y2use = zeroy (ny == 0)
    //        -zeroy (ny == 1)
    Not16(in[0..15]=zeroy, out=negy);
    Mux16(a[0..15]=zeroy, b[0..15]=negy, sel=ny, out=y2use);

    // xandy = x2use & y2use
    And16(a[0..15]=x2use, b[0..15]=y2use, out=xandy);

    // xplusy = x2use + y2use
    Add16(a[0..15]=x2use, b[0..15]=y2use, out=xplusy);

    // preout = xandy (f == 0)
    //          xplusy (f == 1)
    Mux16(a[0..15]=xandy, b[0..15]=xplusy, sel=f, out=preout);

    // out = preout (no == 0)
    //       -preout (no == 1)
    Not16(in[0..15]=preout, out=negpreout);
    Mux16(a[0..15]=preout, b[0..15]=negpreout, sel=no, out=lastout);

    IsZero16(in=lastout, out=zr);
    LastBit16(in[0..15]=lastout, out=ng);
    Pass16(in[0..15]=lastout, out=out);
}